#!/usr/bin/env perl

use strict;

use FindBin qw($Bin);
use lib "$Bin/../lib";

use Mojolicious::Lite;
use DDP;
use DateTime;
use DateTime::Format::Strptime;
use File::Slurp;
use Imager;

# Static files are automatically served from the /public subfolder.

get '/ping' => sub {
    my $self   = shift;
    my $result = {};
    return $self->render(json => { status => 'ok' });
};

get '/calendar/html' => sub {
    my $self = shift;
    return html_calendar($self, DateTime->now());
};

get '/calendar/html/:date' => sub {
    my $self = shift;
    my $dt   = DateTime::Format::Strptime->new(pattern => '%Y-%m-%d')->parse_datetime($self->stash('date'));
    return html_calendar($self, $dt);
};

get '/calendar/png' => sub {
    my $self = shift;
    return $self->render_file(
        'filepath'            => "$Bin/generated_images/current_calendar.png",
        'format'              => 'png',
        'content_disposition' => 'inline',
    ) || $self->render(json => { status => "No calendar available" });
};

get '/calendar/bitmap/:firstrow/:maxnumrows' => sub {
    my $self = shift;

    my $firstrow   = $self->stash('firstrow');
    my $maxnumrows = $self->stash('maxnumrows');

    my $img = Imager->new(file => "$Bin/generated_images/current_calendar.png") or die Imager->errstr;


    $firstrow = 0 if $firstrow < 0;
    my $lastrow = $firstrow + $maxnumrows - 1;
    $lastrow = $img->getheight - 1 if $lastrow > $img->getheight - 1;
    warn "rows = <$firstrow; $lastrow>";

    # output format:
    #    "MM"
    #    <sequence of 1-byte grayscale values>
    my $out = 'MM';    # signature
    foreach my $y ($firstrow .. $lastrow) {
        foreach my $x (0 .. $img->getwidth - 1) {
            my $color = $img->getpixel(x => $x, y => $y);
            my @rgb   = $color->rgba;

            my $grayscale = $rgb[0];
            $out .= chr($grayscale);
        }
    }
    return $self->render(data => $out);
};

get '/' => sub {
    my $self = shift;
    return $self->render(template => 'test', format => 'html');
};

plugin 'TagHelpers';
plugin 'RenderFile';

app->secrets([ $ENV{MOJO_SECRET_PASSPHRASE} ]);
app->start;

########################################################################
sub html_calendar {
    my $self = shift;
    my $dt   = shift;

    $dt->set_locale('en-US');
    return $self->render(
        template => 'calendar',
        format   => 'html',

        # other variables
        date => $dt
    );

}